
/* onplot_movie */

if (notinit(make_movie))   make_movie = 0;
if (notinit(show_inputs)) show_inputs = 0;
if (notinit(space_time))   space_time = 0;
if (notinit(volt_map))       volt_map = 1;
if (notinit(inact_map))     inact_map = 0;
if (volt_map && inact_map) volt_inact = 1
else                       volt_inact = 0;
if (volt_inact)            space_time = 0;
if (notinit(frame_int))     frame_int = 0.0002;
if (notinit(inputs_backgr)) inputs_backgr = 1;	// draw inputs before dendrites

proc draw_time(dtime)

{
  gframe ("time");
  if (dtime < 1e-6 && dtime > -1e-6) dtime = 0;
  gcwidth(0.025);
  gmove(0,0);
  gpen (97);
  gtext ("%-7.4f s",oldtime);
  gmove(0,0);
  gpen (15);
  gtext ("%-7.4f s",dtime);
  oldtime = dtime;
  gframe ("..");
};

proc drawvcolscale(frame,cmin,cmax,unit,colormap)

 /* Draw the scalebar for cacolor display */

{
    local colbarlen,dist,wid;
    local colbase,numcols;
    local x1,y1,x2,y2,x3,y3,x4,y4;
    local dim colors[10][2] = {{0,0,0,16,16,16,32,16,48,16,64,16,80,16,96,100,148,100,0,0}}; 
				/* see colormap in manual */
  gframe (frame);
  gcwidth(0.02);
  colbarlen=0.40;
  wid = 0.03;
  colbase = colors[colormap][0];	/* lowest color in colormap */
  numcols = colors[colormap][1];	/* number of colors used */
  dist = colbarlen/numcols;

  for (i=0; i<numcols; i++){
        gpen(i+colbase); 
	x1 = wid/2;
	y1 = i*dist;
	x2 = x1;
	y2 = (i+1)*dist;
	x3 = -wid/2;
	y3 = y2;
	x4 = x3;
	y4 = y1;
	grect(x1,y1,x2,y2,x3,y3,x4,y4,1);
  };

  gpen (15);
  gmove(-0.05,colbarlen+0.02);
  sprintf (nbuf,"%-.3g %s",cmax,unit);        /* highest value */
  gtext(nbuf);

  gmove(-0.05,-0.03);
  sprintf (nbuf,"%-.3g %s",cmin,unit);          /* lowest value  */
  gtext(nbuf);
  gframe ("..");

};

/* -  -  -  -  -  -  -  -  -  -  -  -  -  -  -  -  -  -  -  -  -  -  */

if (make_movie) {	 /* set up movie and picture components */

  oldtime = 0;
  if (notinit(colormap)) colormap = 2;	/* set colormap for dsgc */
  if (space_time) {
    gframe ("/movie");
    gorigin (0.15,0.35);	/* movie smaller, slightly up */
    gsize (0.7);
    gframe ("time");
    gorigin(0.50,0.035);	/* set time position */
    gframe ("..");
    gframe ("..");
    gframe ("/gc_col_bar");
    gorigin (0.95,0.45);	/* set bar position */
    gframe ("..");
  } else if (volt_inact) {	/* dual display: volts, na inactivation */
    gframe ("/movie");
    gorigin (0.15,0.5);		/* movie smaller, halfway up */
    gsize (0.5);
    gframe ("time");
    gorigin(0.50,0.035);	/* set time position */
    gframe ("..");
    gframe ("..");
    gframe ("/movie2");
    gorigin (0.15,0.05);	/* movie smaller, bottom */
    gsize (0.5);
    gframe ("..");
    gframe ("/gc_col_bar");
    gorigin (0.95,0.45);	/* set bar position */
    gframe ("..");
  } else {
    gframe ("/movie");
    gorigin (0,0);		/* movie fills screen */
    gframe ("time");
    gorigin(0.55,0.035);	/* set time position */
    gframe ("..");
    gframe ("..");
    gframe ("/gc_col_bar");
    gorigin (0.94,0.53);	/* set bar position */
    gframe ("..");
  }; 
  if       (volt_map) drawvcolscale("gc_col_bar",V_min,V_max,"V",colormap)
  else if (inact_map) drawvcolscale("gc_col_bar",0,0.5,"F",colormap);
};

if (notinit(show_stim_volts))	      show_stim_volts = 0;
if (notinit(show_poisson_rate))     show_poisson_rate = 0;
if (notinit(show_actual_release)) show_actual_release = 0;

proc onplot_movie() {
  if (make_movie) {
    if (time >= setxmin && (int(time/ploti+0.0001) % (frame_int/ploti)) == 0) {
      disp |= 32;		/* turn on movie display */
      if (volt_inact) {
        gframe ("/movie");
        draw_time(time);
        if (inputs_backgr && show_inputs) draw_inputs();
        display matching [rec_ct][rec_cn][-1] only color=vcolor 
				  min=V_min max=V_max cmap=colormap;
        if (!inputs_backgr && show_inputs) draw_inputs();
        gframe ("..");
        gframe ("/movie2");		/* show inactivation */
        display matching [rec_ct][rec_cn][-1] only color=nacolor 
				  min 0 max .5 cmap=colormap;
        gframe ("..");
      }
      else {
       gframe ("/movie");
       draw_time(time);
       if (inputs_backgr && show_inputs) draw_inputs();
       if (volt_map) display matching [rec_ct][rec_cn][-1] only color=vcolor 
				  min=V_min max=V_max cmap=colormap;
       if (inact_map) display matching [rec_ct][rec_cn][-1] only color=nacolor 
				  min 0 max .5 cmap=colormap;
       if (!inputs_backgr && show_inputs) draw_inputs();

      //display stim dscale 5;      /* show light stimulus */
      gframe ("..");
     };
     display newpage;
     disp &= 31;		/* turn off movie display */
    };
  };
};
