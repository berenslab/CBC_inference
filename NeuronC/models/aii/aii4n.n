/* square array of aii amacrines */
/* connected by gap junctions */


timinc = 1e-4;
endexp = 0.50;
crit = 1e-8;
ploti = .001;
relax = .10;
drm = 25000;
tempcel = 22;

arrsiz = 15;			/* size of cone array */

scal = 8;
aiisoma = 7;
gjcond =  2e-10;
cond_ampa = 22e-12;
nconn = 9;

qsize =  .004;
darkv  = -.046;
condr = 12e-10;
rseed = 4669;

aiirest = -.055;

nfv = 4;
vsz = 1;
nfc = 0;

natau = 20;
ktau = 28;

nona = 0;		/* =1 -> no Na, K channels in simulation */
nogj = 0;		/* =1 -> no gap junctions in simulation */
stim1 = 0;		/* =1 -> only 1 AII stimulated (not 5)  */
nostim = 0;		/* =1 -> no stimulus, just resting V */
slowr = 1;		/* =1 -> 50 msec rise time */

nsteps=1;		/* number of steps for stim rise time */
risetime=0.001;		/* length of stim rise time */
stimtime = .15;		/* stim start */
stimlen =  .25;		/* stim duration */

aii = 5;
gangc = 30000;

ves_noise = 1;
ch_noise  = 1;

x = setvar();

if (slowr==1)  {
  risetime = .05;
  nsteps = 50;
};

if (stim1==1) {
   qsize += 0.0017;
};

if (nfv==0) {
   darkv -= .0006;
};

totsiz = arrsiz * arrsiz + 1;

include "mid.m";

midarr = midrow(arrsiz);
mida = mid(arrsiz);

rada = int(arrsiz/2);
cent = rada*scal;			/* location of stimulus in microns */

dim aiiarr[arrsiz][arrsiz][4];
dim gconnect[totsiz][nconn];
dim xx[totsiz];


proc congj(xi,xj,xh,xk) {

source = aiiarr[xi][xj][0];		/* source aii */
dest   = aiiarr[xh][xk][0];		/* dest aii */

found=0;
 for (zi=0; zi<nconn; zi++) {		/* check for previous connection */
   if (gconnect[source][zi]==dest) found = 1;
   if (gconnect[source][zi]<0) break;
 };

 if (found == 0) {
    gconnect[source][zi] = dest;		/* remember connection */
    conn [aii][source] to [aii][dest] gj gjcond gnv=1;
 };
 for (zi=0; zi<nconn; zi++) {		/* remember reverse conn */
   if (gconnect[dest][zi]==source) found = 1;
   if (gconnect[dest][zi]<0) break;
 };
 if (found == 0) 
   gconnect[dest][zi] = source;
};



for (i=0; i<totsiz; i++) 
  for (j=0; j<nconn; j++) {
   gconnect[i][j] = -1;
};


x = 0;
for (j=0; j<arrsiz; j++)                                /* make the aii's */
 for (i=0; i<arrsiz; i++) {
    at [aii][x] loc (i*scal,j*scal) sphere dia aiisoma vrest=aiirest;
    if (!nona) {
      if (j>1 && j < arrsiz-1 && i>1 && i < arrsiz-1) {
        at [aii][x] chan Na type 0 maxcond 4e-9 tauh=natau;
      };
    };
    if (j>1 && j < arrsiz-1 && i>1 && i < arrsiz-1) {
      at [aii][x] chan K  type 0 maxcond 4e-10 taun=ktau;
    };
    aiiarr[i][j][0] = x++;
  };

if (!nogj) {                            /* make gap junctions betw aii's */
for (j=0; j<arrsiz; j++)
  for (i=0; i<arrsiz; i++)
    for (k=j-1; k <= j+1; k++)
      for (h=i-1; h <= i+1; h++) {
          if (k<0 || k>=arrsiz) continue;
          if (h<0 || h>=arrsiz) continue;
	  if (i!=h && j!=k) continue; 
          if (i==h && j==k) continue;
          congj (i,j,h,k);
      };
};

/*for (i=0; i<totsiz; i++) {
   print "connect",i;
  for (j=0; j<nconn; j++) {
   print gconnect[i][j];
  };
}; */

at [gangc] sphere dia 20 vrest -.065 rm 10000;


	/* now set up the inputs.  first, noise */

ninp = 10000;
at [ninp] loc (midarr*scal,midarr*scal) sphere dia 7 vrest darkv;

sinp = 20000;
at [sinp] loc (midarr*scal,midarr*scal) sphere dia 7 vrest darkv;

for (ginp=x=0,j=0; j<arrsiz; j++)
  for (i=0; i<arrsiz; i++,x++) {
    if ((j < (rada-1) || j > (rada+1) || i < (rada-1) || i > (rada+1))  
    || (!stim1 && (j!=rada && i!=rada)) || (stim1 && (j!=rada || i!=rada))) { 
     conn [ninp] to [aii][x] synapse open expon=3 thresh=-.045 maxcond=condr
			vrev = -.01 
			vesnoise=ves_noise N=nfv vsize=vsz nfilt2=2 timec2=1.0
			chnoise=ch_noise  N=nfc unit=cond_ampa ename xx[x];
    }
    else {
     conn [sinp] to [aii][x] synapse open expon=3 thresh=-.045 maxcond=condr
			vrev = -.01
			vesnoise=ves_noise N=nfv vsize=vsz nfilt2=2 timec2=1.0
			chnoise=ch_noise  N=nfc unit=cond_ampa ename xx[x];

     conn [aii][x] to [gangc] synapse open expon=8 thresh=-.055 maxcond=12e-10
			vrev = -.01
		/*	vesnoise=ves_noise N=5 vsize=2 nfilt2=2 timec2=.5 /* */
		/* 	chnoise=ch_noise  N=100 unit=cond_ampa	/* */;
     ginp++;

    };
  };


/*  printf ("ganglion cell has %g inputs\n",ginp);  /* */

display size 150;
display calibline 10 loc (0.82,0.25);
display center (56,56);
display xrot 60 yrot -10;
display sphere matching [aii][-1];
display sphere matching [aii][mida-1] dscale 1.01 color 4;
display sphere matching [aii][mida]   dscale 1.01 color 4;
display sphere matching [aii][mida+1] dscale 1.01 color 4;
display sphere matching [aii][mida+arrsiz] dscale 1.01 color 4;
display sphere matching [aii][mida-arrsiz] dscale 1.01 color 4;
display gj     matching [aii][-1] dscale -1 color 14;


	/* stimuli */

stim node [ninp] vclamp darkv start 0 dur 1;

stim node [sinp] vclamp darkv start 0 dur stimtime;

stepdur = risetime/nsteps;
topdur = .02;

if (nostim==1) {
 stim node [sinp] vclamp=darkv start=stimtime dur stimlen;
}
else {
  for (i=0; i<nsteps; i++) {
    stim node [sinp] vclamp=darkv+qsize*i/nsteps
	start=stimtime+risetime*i/nsteps dur stepdur;  /* */
  };
  stim node [sinp] vclamp=darkv+qsize start=stimtime+risetime 
		dur topdur;
  for (i=0; i<nsteps; i++) {
    stim node [sinp] vclamp=darkv+qsize*(nsteps-i)/nsteps
	start=stimtime+risetime+topdur+risetime*i/nsteps dur stepdur;  /* */
  };
};

stim node [sinp] vclamp darkv start stimtime+2*risetime+topdur dur 1;


/* stim node [mida] vclamp -.02 start 0 dur 1; /* stim to measure lambda */

plmax = -.02;
plmin = -.07;

plot V[sinp];
/* plot FA9 xx[mida]; /* */

plot V[aii][mida];
plot V[aii][mida+1];
plot V[aii][mida+2];
plot V[aii][mida+3];
plot V[aii][mida+4];
plot V[aii][mida+5];

plot V[gangc];

include "pcrf.m";

pcolor = 7;

if (!disp) {
  gcwidth (.024);
  if (nona)   ptext (pcolor, .52, .90, "Without Na channels"); 
  if (nogj)   ptext (pcolor, .52, .85, "Without AII gap junctions"); 
  if (stim1)  ptext (pcolor, .52, .80, "With only 1 AII stimulated"); 

  run;
};

