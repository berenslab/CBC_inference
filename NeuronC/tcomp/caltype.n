/* caltype.n: Test of Ca type 1 channel kinetics */

/*  expt = 1  ->    check activation voltage sensitivity
    expt = 2  ->    check inactivation voltage sensitivity
    expt = 3  ->    check kinetics
*/

timinc = 5e-5;
crit   = 1e-8;
endexp = 1.0;
ploti  = 1e-4;

tempcel= 22;

expt = 1;		/* plot rate constants and ninf curve */
expt = 2;		/* plot ctau curve */
expt = 3;		/* plot activation kinetics  */
expt = 4;		/* plot deactivation kinetics */

catyp = 0;		/* L-type Ca channel */
catyp = 1;		/* Markov version */

expt = 1;

dcao = .00767;	/* = 10 / 3 * 2.3   */
			/* Set to .00767 for calib, back to .0023 for use. */
			/* Kawai et al (1996) used 10 mM for data */
x = setvar();

/*----------------------------------------*/

func calcca0m(v, f)

/* Calculate alphac, betac as a function of mv.
*/

{
   local val,x,y;

  if (f==1) {				      /* alpha c */
    y = -0.1 * (v+15.);
    x = exp (y) - 1.;
    if (abs(x) > 1e-5)                        /* singularity at v = -15 mv */
       val = 0.6 * y / x
    else
       val = 0.6;
  }
  else if (f==2)                              /* beta c */
    val = (2 * exp ((v+38) / -18.));
  return (val*1000);
};

/*----------------------------------------*/

func calcca1m(v, f)

/* Calculate alphac, betac as a function of mv.
*/

{
   local val,x,y;

  if (f==1) {				      /* alpha c */
    y = -.1 * (v - -49.);
    x = exp (y) - 1.;
    if (abs(x) > 1e-5)                        /* singularity at v = -15 mv */
       val =  y / x
    else
       val = 1.0;
    val *= 40.;
  }
  else if (f==2)                              /* beta c */
    val = (60. * exp ((v - -69) / -25.));
  return (val);
};

/*----------------------------------------*/

func calcca1h(v, f)

/* Calculate alphach, betach as a function of mv.
*/

{
   local val,x,y;

  if (f==1) {				      /* alpha h */
    val = (12 * exp ((v- -73) / -9.));
  }
  else if (f==2) {                             /* beta h */
    y = -0.05 * (v- -70.);
    x = exp (y) - 1.;
    if (abs(x) > 1e-5)                        /* singularity */
       val = y / x
    else
       val = 1.0; 
    val *= 24;
    val *= 1 - 1 / (1 + ( exp ((v- -50) / -25.)));

  }
  else if (f==3)
    val = 1- 1 / (1 + ( exp ((v- -50) / -25.)));
  return (val);
};

/*----------------------------------------*/

proc onplot () {
   local g, lg;

  if (expt == 3) {
    graph pen (i+1,i+1);
    g = -I[1];
    if (g<0) g=0;
    if (time > 0.01) {
      if (mx < g) mx = g;
      if ((g > 0) && (mx > g) && time > 0.01) {
//         graph (time,-log10(g/mx)*10e-12, mx,g);  /* slope */
         graph (time,g/mx*10e-12, mx,g);	    /* normalized peak */
      };
    };
  };
  if (expt>=4) {
    graph pen (i+1,i+1);
    graph (time,I[1],V[1]);
  };

};

nchan = 1;

at 1 sphere dia 10 rm=500000 vrev -.07 vrest -.03;
at 1 chan Ca type catyp maxcond 250e-12 ename xxx;	/* */


/*----------------------------------------*/

if (expt==1) {

/*  Graph cinf and its slope, ctau, and calpha and cbeta rate functions */
/*  to show activation behavior */


xvmax =  0.03;
xvmin = -0.10;
xstep = 0.001;

 graph X max xvmax min xvmin;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 10e-3 min 0;
 graph Y max 3000 min 0;
 graph Y max 3000 min 0;
 graph init;

 oldc = c= 0;
 vh = -0.044;
 kh = .0100;
 boltz = 0;

 for (v=xvmin; v <= xvmax; v += xstep) {
  
  c = cinf(v,catyp);

  boltz =   1 - 1 / (1 + exp ( (v-vh) / kh) );
  dc = c - oldc;
  slope = dc / (xstep*1000);

   graph (v,boltz,c,slope,ctau(v,catyp),
		calcca1m(v*1000,1),calcca1m(v*1000,2));

 //  graph (v,slope,calcca0m(v*1000,1),calcca0m(v*1000,2));

  oldc = c;
 };

}
else if (expt==2) {

/*  Graph chinf and its slope, chtau, and chalpha and chbeta rate functions */
/*  to show inactivation behavior */

xvmax =  0.03;
xvmin = -0.10;
xstep = 0.001;

 graph X max xvmax min xvmin;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 20e-3 min 0;
 graph Y max 300 min 0;
 graph Y max 300 min 0;
 graph Y max 2 min 0;
 graph init;

 oldh = h = 0;
 vh = -0.065;
 kh = .0078;

 for (v=xvmin; v <= xvmax; v += xstep) {
  
  h = chinf(v,catyp);
  t = chtaub(v,catyp);

  boltz =  1 / (1 + exp ( (v-vh) / kh) );
  dh = h - oldh;
  slope = - dh / (xstep*1000);

   graph (v,boltz,h,slope,t,
		calcca1h(v*1000,1),calcca1h(v*1000,2),calcca1h(v*1000,3));

 //  graph (v,slope,calcca0m(v*1000,1),calcca0m(v*1000,2));

  oldh = h;
 };

}
else if (expt==3) {

endexp = 0.06;

graph X max endexp min 0;
graph Y max 12e-12 min 0;
graph Y max 1.2 min .9;
graph Y max -13 min -11; 
graph init;

//plot I[1] max 1e-11 min -1e-11;
//plot V[1] max 0.04 min -.1;
//plot G(1) xxx max 2 min 0;
//plot G(2) xxx max 2 min 0;
//plot G(3) xxx max 2 min 0;

prevolt  = -.10;
vpulse   = -.05;
tailvolt = -.10;

prestimdur = .01;
stimdur     = .05;
tailcurdur  = .03;
poststimdur = .05;

/* Experiment to test activation at different voltages */

for (i=0; i<4; i++) {
  time = 0;
  mx = -1000;
  for (n=1; n<=nchan; n++) 
     stim node n vclamp=prevolt start=time dur=prestimdur;
  step prestimdur; 
  for (n=1; n<=nchan; n++)
     stim node n vclamp=vpulse start=time dur=stimdur;
  step stimdur; 
  for (n=1; n<=nchan; n++)
     stim node n vclamp=tailvolt start=time dur=tailcurdur;
  step tailcurdur; 
  for (n=1; n<=nchan; n++)
     stim node n vclamp=prevolt start=time dur=poststimdur;
  step poststimdur; 
  vpulse  += .01;
  step .01; 			/* allow last vclamp to turn off */
 };
}
else if (expt==4) {

endexp = 0.06;

graph X max endexp min 0;
graph Y max 10e-12 min -20e-12;
graph Y max 1.2 min .9;
graph Y max -13 min -11; 
graph init;

//plot I[1] max 1e-11 min -1e-11;
//plot V[1] max 0.04 min -.1;
//plot G(1) xxx max 2 min 0;
//plot G(2) xxx max 2 min 0;
//plot G(3) xxx max 2 min 0;

prevolt  = -.10;
vpulse   = -.07;
tailvolt = -.10;

prestimdur = .01;
stimdur     = .05;
tailcurdur  = .03;
poststimdur = .05;

/* Experiment to test activation at different voltages */

for (i=0; i<11; i++) {
  time = 0;
  mx = -1000;
  for (n=1; n<=nchan; n++) 
     stim node n vclamp=prevolt start=time dur=prestimdur;
  step prestimdur; 
  for (n=1; n<=nchan; n++)
     stim node n vclamp=vpulse start=time dur=stimdur;
  step stimdur; 
  for (n=1; n<=nchan; n++)
     stim node n vclamp=tailvolt start=time dur=tailcurdur;
  step tailcurdur; 
  for (n=1; n<=nchan; n++)
     stim node n vclamp=prevolt start=time dur=poststimdur;
  step poststimdur; 
  vpulse  += .01;
  step .01; 			/* allow last vclamp to turn off */
 };

}
else if (expt==5) {

/* Experiment to test tail current deactivation at different voltages */

endexp = 0.2;

graph X max 0.06 min 0;
graph Y max 10e-12 min -20e-12;
graph Y max 0.04 min -.1;
graph init;

//plot I[1] max 2e-11 min -2e-11;
//plot V[1] max 0.04 min -.1;
////plot G xxx max 1e-9 min 0;
//plot G(1) xxx max 2 min 0;
//plot G(2) xxx max 2 min 0;
//plot G(3) xxx max 2 min 0;

prevolt  = -.1;
vpulse   = -.01;
tailvolt =  -.1;

prestimdur = .01;
stimdur     = .005;
tailcurdur  = .02;
poststimdur = .15;

/* Experiment to test activation at different voltages */

for (i=0; i<9; i++) {
  time = 0;
  for (n=1; n<=nchan; n++)
     stim node n vclamp=prevolt start=time dur=prestimdur;
  step prestimdur; 
  for (n=1; n<=nchan; n++)
     stim node n vclamp=vpulse start=time dur=stimdur;
  step stimdur; 
  for (n=1; n<=nchan; n++)
     stim node n vclamp=tailvolt start=time dur=tailcurdur;
  step tailcurdur;
  for (n=1; n<=nchan; n++)
     stim node n vclamp=prevolt start=time dur=poststimdur;
  step poststimdur; 
  tailvolt += .01;
  step .01; 			/* allow last vclamp to turn off */
};

};
