/* Plot activation fractions of Na, Kdr, and KA channels */
/*  at different voltages */


timinc = 5e-5;
crit   = 1e-8;
endexp = 1.0;
ploti  = 1e-4;

tempcel= 22;

expt = 1;		/* plot rate constants and ninf curve */

x = setvar();

/*----------------------------------------*/

func calcminf_v(v)

/* from van Rossum and Smith, (2000) */

{
   local a, b, mv, x, y, vo;

   mv = v * 1000;
   vo = mv + 30;

   y  = -0.1 * vo;
   x  = exp (y) - 1;
   if (abs(x) > 1e-5) a = y / x
   else               a = 1.0;
   a *= 1000;
   b = 4000 * exp ((mv + 55)/ -18);

   return a / (a + b);
};

/*----------------------------------------*/

func calcminf_b(v)

/*  from Benison, Robinson and Chalupa, unpub, (1998) */

{
   local a, b, mv, x, y, vo;

   mv = v * 1000;
   vo = mv + 29;

   y  = -0.18 * vo;
   x  = exp (y) - 1;
   if (abs(x) > 1e-5) a = y / x
   else               a = 1.0;
   a *= 2.778;
   b =  6 * exp ((mv + 45)/ -15);

   return a / (a + b);
};

/*----------------------------------------*/

func calcninf_v(v)

/* Calculate KA activation. */
/*  from van Rossum and Smith, (2000) */

{
   local a, b, mv, x, y, vo;

   mv = v * 1000;
   vo = mv + 90;

   y  = -0.1 * vo;
   x  = exp (y) - 1;
   if (abs(x) > 1e-5) a = 10.7 * y / x
   else               a = 10.7;
   b = 17.8 * exp (-0.1 * (mv + 30));

   return a / (a + b);
};

/*----------------------------------------*/

func calcdinf_v(v)

/* Calculate KA activation. */
/*  from van Rossum and Smith, (2000) */

{
   local a, b, mv, x, y, vo;

   mv = v * 1000;

   a = 7.1 * exp (-.05 * (mv + 70));
   b =  107 / (exp (-0.1 * (mv + 40)) + 1);

   return a / (a + b);
};

/*----------------------------------------*/

func calcninf_b(v)

/* Calculate KA activation. */
/*  from Benison, Robinson and Chalupa, unpub, (1998) */

{
   local a, b, mv, x, y, vo;

   mv = v * 1000;
   vo = mv + 15;

   y  = -0.12 * vo;
   x  = exp (y) - 1;
   if (abs(x) > 1e-5) a = y / x
   else               a = 1.0;
   a *= 1000 / 6;
   b = 1000 * 0.05 * exp (-(mv + 1) / 30);

   return a / (a + b);
};

/*----------------------------------------*/

func calcdinf_b(v)

/* from Benison, Robinson and Chalupa, unpub, (1998) */
/* almost the same as Na_h */

{
   local a, b, mv, x, y, vo;

   mv = v * 1000;
   vo = mv + 62;

   return  (1 / (1 + exp (vo / 6.35)));
};

/*----------------------------------------*/

/* The model: a sphere and channels */

nchan = 1;

at 1 sphere dia 10 rm=500000 vrev -.07 vrest -.07;
at 1 chan Na type 0 
        offsetm = 0.000 
	ename nahh;
at 1 chan K type 0 maxcond 300e-12 ename kdr;	/* */
at 1 chan K type 2 maxcond 300e-12 ename ka;	/* */

/*----------------------------------------*/

if (expt==1) {

/*  Graph minf and its slope, ntau, and calpha and cbeta rate functions */
/*  to show activation behavior */

xvmax =  0.03;
xvmin = -0.10;
xstep = 0.001;

 graph X max xvmax min xvmin;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph Y max 1 min 0;
 graph init;

 gmove (.20,.95);
 gcwidth(.025);
 gtext ("Na and KA channel activation");

 for (v=xvmin; v <= xvmax; v += xstep) {
  
  m = minf(v,nahh);
  mv = calcminf_v(v);		/* from van Rossum and Smith (2000) */
  mb = calcminf_b(v);		/* from Benison, Robinson, and Chalupa (1998)*/
  n = ninf(v,kdr) + .01;
  ncv = calcninf_v(v);		/* from van Rossum and Smith (2000) */
  ncb = calcninf_b(v);		/* from Benison, Robinson, and Chalupa (1998)*/
  h = hinf(v,nahh);
  dcv = calcdinf_v(v)+.01;		/* from van Rossum and Smith (2000) */
  d = dinf(v,ka);
  dcb = calcdinf_b(v);		/* from Benison, Robinson, and Chalupa (1998)*/

   graph (v,m,mv,mb,n,ncv,ncb,h,dcv,d,dcb);

 };

};

