/* Test of reversal potential in a dendrite */
/*  with excitatory and inhibitory synaptic inputs */ 

/* Test synaptic calcium sensitivity */

timinc   = 1e-4;
ploti    = 1e-4;
tempcel  = 35;
implicit = 1;
crit     = 1e-10; 
plsep    =1;

grest = -.060;
drm=100000;

electr_resistance = 1e6;
electr_dia = 2;

plot_freq = 1;		/* turn on plot of spike frequency */

expt = "vclamp";

x = setvar();		/* get user-defined parameters from command line */

/*---------------------------------------------*/


/* set parameters for experiments here */

/*---------------------------------------------*/

proc runstim(starttime)

{
    stim node [bipl][1] vclamp=-.035 start=starttime dur=stimdur;
    stim node [bipl][1] vclamp=bpvrev start=starttime+stimdur dur=endexp;

    stim node [bipl][2] vclamp=-.035 start=starttime+idelay dur=stimdur;
    stim node [bipl][2] vclamp=bpvrev start=starttime+stimdur+idelay dur=endexp;
};

/*---------------------------------------------*/

func runclamp(starttime)
{
    stim node electr  vclamp=prevolt start=starttime dur=predur;
    stim node electr  vclamp=vpulse start=starttime+predur dur=vclampdur;
    stim node electr  vclamp=prevolt start=starttime+predur+vclampdur
					dur=postdur;
    return (starttime+predur+vclampdur+postdur);
};


/*---------------------------------------------*/

/* construct the neuron */

gang    = 1;
bipl    = 2;
soma    = 0;
electr  = 100;

dencabl_dia = .5;
denseglen   = 20;
gvrev   = -0.03;
bpvrev  = -0.06;

/* make the ganglion cell */

for (i=0; i<20; i++) {
  at [gang][i] loc (i*denseglen,0);
  conn [gang][i] to [gang][i+1] cable dia dencabl_dia vrev=gvrev ;
};
at [gang][i] loc (i*denseglen,0);
at [gang][soma] sphere dia 25 vrest=grest vrev= -0.06;

/* make the bipolar cell */
at [bipl][1] sphere dia 5 vrest grest vrev bpvrev;
at [bipl][1] chan Ca maxcond=2e-8 capump vmax 1e-7;

at [bipl][2] sphere dia 5 vrest grest vrev bpvrev;

/* connect bipolar to ganglion cell */
conn [bipl][1] loc(i*denseglen,0) to [gang][20] 
		synapse open 
		sens Ca vgain=1
		maxcond 2000e-12
		vrev = 0
		thresh = -0.045;

conn [bipl][2] loc(i*denseglen,0) to [gang][20] 
		synapse open
		maxcond 2000e-12
		vrev = -0.07
		thresh = -0.045;

at electr sphere dia electr_dia rm=1e6 vrest=0 vrev= 0; /* electr capac.*/
conn electr to [gang][soma] resistor electr_resistance; 

/*-------------------------------*/

if (expt=="vclamp") {

/* run voltage clamp experiment */

vstart      = -.08;
vend        =  .041;
vstep       =  .01;

prevolt     = -.08;

predur    = .010;
vclampdur = .080;
postdur   = .010;

stimdur     =  .005;
idelay      =  .03;

  plg   = 0.04;
  offtr = 0;
  offb  = -0.06;
  plot V[bipl][1]   max (1-offtr)*plg+offb 
                 min (0-offtr)*plg+offb
                       pen 3 plname "Vbip1" plnum 4 plsize .5;

  plot Ca(1) [bipl][1]   max 1e-6 min 0 pen 3 plname "Cabip1" plnum 5 plsize .5;
  plot V[bipl][2]   max (1-offtr)*plg+offb 
                 min (0-offtr)*plg+offb
                       pen 6 plname "Vbip2" plnum 4 plsize .5;

  plg   = 0.12;
  offtr = 0;
  offb  = -0.08;
  plot V[gang][20] max (1-offtr)*plg+offb 
                   min (0-offtr)*plg+offb
                       pen 4 plname "Vdend" plnum 1 plsize 1;

  plg   = 40e-12;
  offtr = .2;
  offb  = 0;
  plot I[electr] max (1-offtr)*plg+offb 
                 min (0-offtr)*plg+offb
                       pen 1 plname "Isoma" plnum 2 plsize 1;
  plg   = .12;
  offtr = 0;
  offb  = -0.08;
  plot V[gang][soma] max (1-offtr)*plg+offb 
                 min (0-offtr)*plg+offb
                       pen 2 plname "Vsoma" plnum 1 plsize 1;

  for (i=0,vpulse=vstart; vpulse<=vend; i++, vpulse += vstep) {
    time = 0;
    runstim(time+predur+.030);
    endexp=runclamp(time);
    step endexp;
  };
};

/*---------------------------------------------*/
