/* Test of spike generator */ 
/* single compartement */
/* based on Fohlmeister Miller params. */

/* scompa.n: Script to show spike adaptation. */
/* scompb.n: scompa.n, tuned to 37 deg */
/* scompc.n: Derived from scompa.n @ 22 deg C subst "ndensity" for "density"*/
/* scompd.n: scompc.n, tuned to 37 deg */

timinc = 1e-5;
ploti=1e-5;
tempcel = 35;
implicit=0;
crit=1e-10; 

dpcana=0;
dpnaca=0;

dpcak=0;
dpkca=0;

dpnak=0;
dpkna=0;
dcavoff=0;
calcnernst=0;

dqm = 3;

vna =  0.040; 
vk  = -0.080; 
vcl = -.07;

grest = -.063;
dcai = 100e-9; // calcium concentration inside at t0
drm=10000;

na_mult =    1;   // Na
ca_mult =    1;   // L-type Ca
k0_mult =    1;   // Kdr
k3_mult =    1;   // KA
skca1_mult = 1;   // sKCa, apamin-sensitive
skca5_mult = 1;   // sKCa, apamin-insensitive
bkca_mult =  1;   // bKCa
ifactor =   .10;
ifactor =   .05;

plot_freq = 1;

x = setvar();

/*---------------------------------------------*/

func qcond (cond) {

/* convert from "density" S/cm2 to "ndensity" N/um2 at 22 deg C */

  return (1e8 * cond * exp(log(1.4)*(22-6.3)/10));
};

/*---------------------------------------------*/

soma = 1;
at soma sphere dia 25 vrest grest vrev -.06;
/* area=2000 um^2 =20*10^-6 cm ^2
/* note: density in -S-/cm2 */


at soma chan 
   Na type 0 ndensity (1-ifactor)*170e-3*na_mult/qcond(dnau) tauh=1
	//chnoise=1 
       ename naxx;


at soma chan 
   Na type 0 ndensity ifactor*170e-3*na_mult/qcond(dnau) tauc=30 taud=30;


catau = 50e-3;
//ddca = 1e-12;
ddca = 2e-6;

at soma chan 
       Ca type 0 ndensity ca_mult*1e-3/qcond(dcalu) offset= -.005
	//chnoise=1 
        capump vmax=5e-9/catau km=5e-6
	cshell 20 
        ename cachan;

at soma chan 
       K type 0 
	ndensity k0_mult* 12e-3/qcond(dku) 
	//chnoise=1
	ename khh;  

at soma chan 
       KCa type 0 
	ndensity skca1_mult*.03e-3/qcond(dkcasu) 
	k1=1e-7
	k2=1e-7
	//chnoise=1 
	ename skca1;

at soma chan 
       KCa type 5 
	ndensity skca5_mult*.1e-3/qcond(dkcasu) 
	//chnoise=1 
	ename skca2;

/*
at soma chan 
       KCa type 2 
	ndensity bkca_mult*5e-3/qcond(dkcabu) 
	//chnoise=1 
	ename bkca;
*/

at soma chan /* K_A channel */
       K type 2  offset= .01 
	ndensity k3_mult*100e-3/qcond(dkau) 
	//chnoise=1
	ename ka; 

  /* current clamp, then record spike train and [Ca] */

  waittime = 0.1; // Let the system relax before stimulus.
  stimdur  = 0.2;
  afterstim = 0.10;
  nstim=4;
  ndstim=0;
  endexp=waittime+nstim*(stimdur+afterstim)+ndstim*stimdur;
//endexp = .4;
  stimampl = 20e-12;

for (i=1; i<=nstim; i++) {
    stim node soma cclamp i*stimampl start waittime+(i-1.0)*(stimdur+afterstim) 
		dur stimdur; 
};
for (i=1; i<=ndstim; i++) {
    stim node soma cclamp nstim*stimampl-i*stimampl start waittime+(nstim+i-1.0)*stimdur dur stimdur; 
};

proc onplot () {                /* this procedure runs at plot time */

  if (plot_freq) onplot2();
};

if (plot_freq) {

/* Process intracellular voltage to find spike times, */
/* inter-spike intervals, and instantaneous spike */
/* frequencies. */

spikrate = 0;
spikint = 0;
spiktim = 0;
spikthresh = -.04;
spikyet = 0;

vh = 0;
oldvh = 0;
oldspiktim = 0;

func spikplot (nplot, xval, yval)
{
   if (yval > 50) retval = 7
   else if (yval > 20)  retval = 5
   else                 retval = 6;
   if (yval < 10) retval = -1;
   return (retval);
};

proc onplot2 ()

/* calculate instantaneous frequency from inter-spike interval */

{
  vh = (V[soma] > spikthresh);

  if (vh && !oldvh) {           /* spike here */
     spiktim = time;
     if (spikyet) {
        spikint = spiktim - oldspiktim + 1e-12;
        spikrate = 1 / spikint;
     };
     spikyet = 1;
     oldspiktim = spiktim;
  }
  else spikrate = 0;
  oldvh = vh;
};

};

 plot V[soma] pen 4 max .04 min -.08;
//  plot G(I) ka pen 5 max 0 min -1e-10;
//  plot I[soma] pen 5 max 1e-9 min -1e-9;
//  plot G(I) naxx ; /*I_na*/
//  plot G(I) khh; /* I_k_dr */
//  plot G kca max 10e-9 min 0; /* I Kca */
//  plot Ca(I) [soma]; /* ica*/
//  plot Ca(1) [soma] max 3e-6 min 0; /*[ca]*/
  plot Ca(1)  [soma] max 3e-6 min 0 pen 2; /*[ca]*/
//  plot Ca(IP)  [soma] max 1e-12 min 0 pen 3; /*[ca]*/
//  plot Ca(10) [soma] max 1e-6 min 0 pen 2; /*[ca]*/
//  plot cabuf(1) [soma] max 30e-6 min 0 pen 6; /*[ca]*/
//  plot G(I) ka max 1e-9 min -1e-9; /* IKA*/
// plot V[hillock] pen 3;
// plot V[axon1] pen 3;
// plot G khh pen 5 max 100e-9 min 0;
// plot G ka pen 2 max 1e-9 min 0;
// plot Ca(vrev) [soma] max .1 min 0; 
// plot Ca(5)  [soma] pen 5 max 3e-6 min 0;
//  plot Ca(99) [soma] pen 5 max 3e-6 min 0;
// plot  G kca pen 6 max 200e-12 min 0;

if (plot_freq) plot S spikrate max 500 min 0 Char 'o' size .01 vpen spikplot;

run;

