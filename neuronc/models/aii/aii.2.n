/* square array of aii amacrines */
/* connected by gap junctions */


timinc = 1e-4;
crit = 1e-8;
ploti = .0001;
relax = .10;
drm = 10000;
tempcel = 22;

arrsiz = 12;			/* size of cone array */

scal = 10;
aiisoma = 7;
gjcond =  2e-10;
nconn = 9;
cond_ampa = 22e-12;

qsize =  .005;
darkv  = -.042;

aiirest = -.05;

nfv = 0;
nfc = 0;

nonak = 1;		/* =1 -> no Na, K channels in simulation */
nogj = 0;		/* =1 -> no gap junctions in simulation */
stim1 = 1;		/* =1 -> only 1 AII stimulated (not 5)  */

ves_noise = 1;
ch_noise  = 1;

x = setvar();

totsiz = arrsiz * arrsiz + 1;

include "mid.m";

midarr = midrow(arrsiz);
mida = mid(arrsiz);

rada = arrsiz/2;
cent = rada*scal;			/* location of stimulus in microns */

dim aiiarr[arrsiz][arrsiz][4];
dim gconnect[totsiz][nconn];
dim xx[totsiz];

/*-----------------------------------------------------------*/

proc congj(xi,xj,xh,xk) {

source = aiiarr[xi][xj][0];		/* source aii */
dest   = aiiarr[xh][xk][0];		/* dest aii */

found=0;
 for (zi=0; zi<nconn; zi++) {		/* check for previous connection */
   if (gconnect[source][zi]==dest) found = 1;
   if (gconnect[source][zi]<0) break;
 };

 if (found == 0) {
    gconnect[source][zi] = dest;		/* remember connection */
    conn [source] to [dest] gj gjcond gnv=1;
 };
 for (zi=0; zi<nconn; zi++) {		/* remember reverse conn */
   if (gconnect[dest][zi]==source) found = 1;
   if (gconnect[dest][zi]<0) break;
 };
 if (found == 0) 
   gconnect[dest][zi] = source;
};

/*-----------------------------------------------------------*/


for (i=0; i<totsiz; i++) 
  for (j=0; j<nconn; j++) {
   gconnect[i][j] = -1;
};

x = 0;
for (j=0; j<arrsiz; j++)                                /* make the aii's */
 for (i=0; i<arrsiz; i++) {
    at [x] loc (i*scal,j*scal) sphere dia aiisoma vrest=aiirest;
    if (!nonak) {
      at [x] chan Na type 0 maxcond 4e-9  tauh .02; 
      at [x] chan K  type 0 maxcond 4e-10 taun .02;
    };
    aiiarr[i][j][0] = x++;
  };

if (!nogj) {                            /* make gap junctions betw aii's */
for (j=0; j<arrsiz; j++)
  for (i=0; i<arrsiz; i++)
    for (k=j-1; k <= j+1; k++)
      for (h=i-1; h <= i+1; h++) {
          if (k<0 || k>=arrsiz) continue;
          if (h<0 || h>=arrsiz) continue;
	  if (i!=h && j!=k) continue; 
          if (i==h && j==k) continue;
          congj (i,j,h,k);
      };
};

/*for (i=0; i<totsiz; i++) {
   print "connect",i;
  for (j=0; j<nconn; j++) {
   print gconnect[i][j];
  };
}; */

gangc = 30000;
at [gangc] sphere dia 20 vrest -.055;


	/* now set up the inputs.  first, noise */

ninp = 10000;
at [ninp] loc (midarr*scal,midarr*scal) sphere dia 7 vrest -.04;

sinp = 20000;
at [sinp] loc (midarr*scal,midarr*scal) sphere dia 7 vrest -.04;

for (ginp=x=0,j=0; j<arrsiz; j++)
  for (i=0; i<arrsiz; i++,x++) {
    if ((j < (rada-1) || j > (rada+1) || i < (rada-1) || i > (rada+1))  
    || (!stim1 && (j!=rada && i!=rada)) || (stim1 && (j!=rada || i!=rada))) { 
     conn [ninp] to [x] synapse open expon=3 thresh=-.045 maxcond=12e-10
			vrev = -.01 
			vesnoise=ves_noise N=nfv vsize=2 nfilt2=2 timec2=1
			chnoise=ch_noise  N=nfc unit=cond_ampa ename xx[x];
    }
    else {
     conn [sinp] to [x] synapse open expon=3 thresh=-.045 maxcond=12e-10
			vrev = -.01
			vesnoise=ves_noise N=nfv vsize=2 nfilt2=2 timec2=1
			chnoise=ch_noise  N=nfc unit=cond_ampa ename xx[x];

     conn [x] to [gangc] synapse open expon=3 thresh=-.045 maxcond=12e-10
			vrev = -.01
		/*	vesnoise=ves_noise N=5 vsize=2 nfilt2=2 timec2=.5 /* */
		/* 	chnoise=ch_noise  N=100 unit=cond_ampa	/* */;
     ginp++;

    };
  };


/*  printf ("ganglion cell has %g inputs\n",ginp);  /* */

display size 250;
display calibline 10;
display center (100,100);
display sphere matching [-1][-1];
display gj matching [-1][-1] dscale -1;


	/* stimuli */

stimtime = 00;
stimlen =  .1;

stim node [ninp] vclamp darkv start 0 dur 1;

stim node [sinp] vclamp darkv start 0 dur stimtime;

stim node [sinp] vclamp=darkv+qsize start=stimtime dur stimlen;  /* */

stim node [sinp] vclamp darkv start stimtime+stimlen dur 1;


/* stim node [mida] vclamp -.02 start 0 dur 1; /* stim to measure lambda */

plmax = .00;
plmin = -.06;

plot V[ninp];
plot V[sinp];

plot V[mida];
plot V[mida+1];
plot V[mida+2];
plot V[mida+3];
plot V[mida+4];
plot V[mida+5];

plot V[gangc];

include "pcrf.m";

pcolor = 7;

if (nonak)  ptext (pcolor, .65, .90, "Without Na, K channels"); 
if (nogj)   ptext (pcolor, .65, .85, "Without AII gap junctions"); 
if (stim1)  ptext (pcolor, .65, .80, "With only 1 AII stimulated"); 

endexp = 0.20;
run;
