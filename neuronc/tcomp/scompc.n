/* Test of spike generator */ 
/* single compartement */
/* based on Fohlmeister Miller params. */

/* scompa.n: Script to show spike adaptation. */
/* scompc.n: Derived from scompa.n @ 22 deg C subst "ndensity" for "density"*/

timinc = 1e-5;
ploti=1e-4;
tempcel = 22;
implicit=0;
crit=1e-10; 

//dpnak=0;
//dpkna=0;
//dcavoff=0;
//calcnernst=0;

//vna =  0.035; 
//vk  = -0.075; 
//vcl = -.07;

grest = -.063;
dcai = 100e-9; // calcium concentration inside at t0
drm=10000;

na_mult =  1;
ca_mult =  2;
k0_mult =  .3;
k2_mult =  5.; // kca
k3_mult =  7; // ka

plot_freq = 1;

x = setvar();

func qcond (cond) {

/* convert from "density" S/cm2 to "ndensity" N/um2 at 22 deg C */

  return (1e8 * cond * exp(log(1.4)*(22-6.3)/10));
};

soma = 1;
at soma sphere dia 25 vrest grest vrev -.06;
/* area=2000 um^2 =20*10^-6 cm ^2
/* note: density in -S-/cm2 */

at soma chan 
   Na type 0 ndensity na_mult*140e-3/qcond(dnau)        
//   Na type 0 density 140e-3*na_mult        
//   Na density 50e-3*na_mult //Fohl
//	chnoise=1 
       ename naxx;

at soma chan 
       Ca taum 1 offset .00 
	ndensity ca_mult*3e-3/qcond(dcalu)
//	density ca_mult*3e-3 // Fohl
//	chnoise=1 
	capump vmax 1e-6 km=.5e-6 //caexch kex .5e-9 
	cabuf vmax 1e8 kd .5e-6 btot 3e-6 btoti 10e-6
//	cbound 1
	cshell 10
       ename cachan;

at soma chan 
       K type 0 
	ndensity k0_mult*13e-3/qcond(dku)
//	density k0_mult* 13e-3 
//	chnoise=1
	ename khh;  
	
at soma chan 
       KCa type 0 
	ndensity k2_mult*10e-3/qcond(dkcasu)
//	density k2_mult*10e-3 
	k1=2e-4 k2=5e-4 d1=0 d2=0 
//	chnoise=1 
	ename kca;
	
at soma chan /* K_A channel */
       K type 2 
	ndensity k3_mult*35e-3/qcond(dkau)
//	density k3_mult*35e-3
//	chnoise=1
	ename ka; 

  /* current clamp, then record spike train and [Ca] */

  waittime = 0.1; // Let the system relax before stimulus.
  stimdur  = 0.4;
  afterstim = 0.00;
  nstim=3;
  endexp=waittime+nstim*stimdur+afterstim;
//endexp = .1;
  stimampl = 30e-12;

for (i=1; i<=nstim; i++) {
    stim node soma cclamp i*stimampl start waittime+(i-1.0)*stimdur dur stimdur; 
};

proc onplot () {                /* this procedure runs at plot time */

  if (plot_freq) onplot2();
};

if (plot_freq) {

/* Process intracellular voltage to find spike times, */
/* inter-spike intervals, and instantaneous spike */
/* frequencies. */

spikrate = 0;
spikint = 0;
spiktim = 0;
spikthresh = -.03;
spikyet = 0;

vh = 0;
oldvh = 0;
oldspiktim = 0;

func spikplot (nplot, xval, yval)
{
   if (yval > 50) retval = 7
   else if (yval > 20)  retval = 5
   else                 retval = 6;
   if (yval < 10) retval = -1;
   return (retval);
};

proc onplot2 ()

/* calculate instantaneous frequency from inter-spike interval */

{
  vh = (V[soma] > spikthresh);

  if (vh && !oldvh) {           /* spike here */
     spiktim = time;
     if (spikyet) {
        spikint = spiktim - oldspiktim + 1e-12;
        spikrate = 1 / spikint;
     };
     spikyet = 1;
     oldspiktim = spiktim;
  }
  else spikrate = 0;
  oldvh = vh;
};

};

 plot V[soma] pen 4 max .01 min -.08;
//  plot I[soma] pen 5 max 1e-9 min -1e-9;
//  plot G(I) naxx ; /*I_na*/
//  plot G(I) khh; /* I_k_dr */
  plot G kca max 10e-9 min 0; /* I Kca */
//  plot Ca(I) [soma]; /* ica*/
  plot Ca(1) [soma] max 3e-6 min 0; /*[ca]*/
//  plot cabuf(1) [soma] max 30e-6 min 0 pen 6; /*[ca]*/
//  plot G(I) ka max 1e-9 min -1e-9; /* IKA*/
// plot V[hillock] pen 3;
// plot V[axon1] pen 3;
// plot G khh pen 5 max 100e-9 min 0;
// plot G ka pen 2 max 1e-9 min 0;
// plot Ca(vrev) [soma] max .1 min 0; 
// plot Ca(5)  [soma] pen 5 max 3e-6 min 0;
  plot Ca(99) [soma] pen 5 max 3e-6 min 0;
// plot  G kca pen 6 max 200e-12 min 0;

if (plot_freq) plot S spikrate max 500 min 0 Char 'o' size .01 vpen spikplot;

run;

