/* tcomp21c:  flash response */

/* script to test cone response */

timinc = 1e-4;
ploti = 1e-3;
endexp = 0.3;
drm = 10000;
plsep = 1;

expt = 3;

n=5;

if (expt==1) {
/* check rhod, rhod2, rstar, gpr, pde */

stimstart = .001;
endexp = .02;
stimdur = .0001;
ploti = .0001;
 
}
else if (expt==2) {
/* check rstar, gpr, pde, cond */

  endexp = .1;

}
else if (expt==3) {
/* check pde, cycg, cond, ca, cax, gcyc */

  endexp = 1;
}
else if (expt==4) {
/* check cond */

  endexp = .3;
  n=1;

};


if (notinit(n)) n = 10;
dim rn[n];

if (notinit(stimdur))   stimdur   = 0.1;
if (notinit(stimstart)) stimstart = 0.5;
if (notinit(flashb))    flashb    = 1e3;
if (notinit(bginten))   bginten   = 2e4;
if (notinit(scontrast)) scontrast = 0.1;
if (notinit(clamp))     clamp = 1;

m = 2;

for (i=0; i<n; i++) {
  at [i] sphere dia 10;
  at [i] cone (i,0) linit=bginten*2^(m*i) ename rn[i];
  stim cone i inten=bginten*2^(m*i) start=0 dur=endexp;		// background
  //stim cone i inten=flashb/stimdur start=stimstart dur=stimdur;
  if (scontrast<0) stim cone i inten=bginten*2^(m*i)*scontrast start=stimstart dur=stimdur
  else stim cone i inten=bginten*2^(m*i)/stimdur*scontrast start=stimstart dur=stimdur;
  if (clamp) stim node i vclamp -.06 start 0 dur 5;
};


cond  = 0;
rhod  = 1;
rhod2 = 2;
rstar = 3;
gpr   = 4;
pde   = 5;
gcyc  = 6;
cycg  = 7;
ca    = 8;
cax   = 9;
cab   = 10;
rhodk = 11;

/*plot I[n] max 40e-12 min 0; */

plmax = -.02;
plmin = -.07;

if (expt==1) { 
  for (i=0; i<n; i++) {
     plot G(rhod)  rn[i] max 1 min -.3 pen 1 plnum 1;
     plot G(rhod2) rn[i] max 1 min -.3 pen 2 plnum 2;
     plot G(rstar) rn[i] max 1 min -.3 pen 3 plnum 3;
     plot G(gpr)   rn[i] max 1 min -.3 pen 4 plnum 4;
     plot G(pde)   rn[i] max 1 min -.3 pen 5 plnum 5;
  }; 
};

if (expt==2) { 
  for (i=0; i<n; i++) {
    plot I[i] max 0 min -80e-12 pen 1 plnum 1;
    plot G(rstar) rn[i] max 1 min -.3 pen 3 plnum 3;
    plot G(gpr)   rn[i] max 3 min -1 pen 4 plnum 4;	
    plot G(pde)   rn[i] max 3 min -1 pen 5 plnum 5;
  }; 
};

if (expt==3) { 
  for (i=0; i<n; i++) {
    if (clamp) plot I[i] max 0 min -100e-12 pen 1 plsize 2 plname "I" plnum 1
    else       plot V[i] max -0.01 min -0.05 pen 1 plsize 2 plname "V" plnum 1;
    plot G(cycg) rn[i] max 2 min 0 pen 2 plname "cycg" plnum 2;
//    plot G(gpr) rn[i] max 2 min 0 pen 4 plname "Gpr" plnum 4;
    plot G(pde)  rn[i] max 5 min 0 pen 5 plname "pde" plnum 5;
    plot G(gcyc) rn[i] max 1.5 min 0 pen 6 plname "gcyc" plnum 6;
    plot G(ca)   rn[i] max 0.3 min 0 pen 3 plname "Ca" plnum 3;
    plot G(cax)  rn[i] max 0.5 min 0 pen 8 plname "Cax" plnum 8;
    plot G(cab)  rn[i] max 20.0 min 0 pen 9 plname "Cab" plnum 9;
    plot G(rhodk)rn[i] max 1.0 min 0 pen 10 plname "Rhodk" plnum 10;
  };
};

if (expt==4) { 
  for (i=0; i<n; i++) {
    plot I[i] max -65e-12 min -67e-12 pen 1 plnum 1;
  };
};

run; 

