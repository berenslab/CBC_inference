/* rbc_aii.n: model of RBC->AII synapse */

include "nc_colors.h";

/* How to run; typical command lines:

  nc --ivplot 0 -v rbc_aii.n | vid			      // run vid window of VC protocol 
  neurc --ivplot 0 rbc_aii.n 				      // same, run vid window of VC protocol 

  nc --ivplot 0 --stimdur 0.5 -v rbc_aii.n | vid	      // run longer VC pulse dur

  nc --ivplot 1 -v rbc_aii.n | vid 			      // run I/V plot
  nc --ivplot 1 -v rbc_aii.n | vid -c > rbc_aii_ivplot.ps     // make .ps of I/V plot
  nc --ivplot 1 rbc_aii.n > rbc_aii_ivplot.r 		      // make text file of I/V plot traces

  nc --ivplot 1 --catype 2 --stimdur 1 -v rbc_aii.n | vid     // use slowly inactivating Ca chan
  nc --ivplot 0 --catype 2 --stimdur 1 -v rbc_aii.n | vid

  nc --ivplot 0 --vnoise 1 rbc_aii.n > rbc_aii_vnois.r        // run model with vesicle noise
  nc --ivplot 0 --vstart -0.05 --vstop -0.049 --stimdur 0.5 -v rbc_aii.n | vid // run VC pulse at one voltage

*/
   
timinc = 5e-6;
crit   = 1e-10;
ploti  = 1e-5;

expt = 1;		/* -> 1, plot activation kinetics  */

ivplot = 1;

rbc = 10;		/* node numbers for rbc, aii */
aii = 20;

dscavg = 1e6;		/* synaptic release gain */
dscaeg = 1;		/* synaptic release exponential gain */

vnoise = 0;		/* => 1 -> turn on vesicle noise */
maxrrpool = 20;		/* max size of readily releaseable pool */
maxsynrate = 50;	/* max sustainable (replenishment) rate */

x = setvar();		/* set variables from command line */

/*----------------------------------------*/

flag = 0;

proc onplot()

/* procedure to run automatically at plot time */

{
  if (flag) {
   if (expt==1) {
     if (prestimdur+0.0001<time && time < (prestimdur+stimdur)) {
        Irec = I[rbc];
        if (Irec < Irbc) Irbc = Irec;
        Irec2 = I[aii];
        if (Irec2 < Iaii) Iaii = Irec2;
     };
   };
  };
};

/*----------------------------------------*/

/* RBC -> AII amacrine synapse */

at [rbc] sphere dia 4 rm=1e8 vrev -.07 vrest -0.07;		/* make RBC with very high Rm */

if (notinit(catype)) catype = 1;

if (catype==1)
   at [rbc] chan Ca type 1 density 8e-3 offsetm = -0.005    			/* L-type, no inactivation */
else if (catype==2)
   at [rbc] chan Ca type 2 density 10e-3 tauc = 100 taud = 100 offsetm = 0.040  /* T-type, shifted 40 mV, slow inact */
else if (catype==6)
   at [rbc] chan Ca type 6 density 4e-3  tauc = 100 taud = 100 offsetm = 0.035;  /* T-type, shifted 35 mV, slow inact */

at [rbc] cacomp capump vmax=20e-6 km=20e-6;
 // capump vmax=10e-6 km=10e-6 cabuf vmax 4e7 kd 2e-6 btot 10e-6 btoti 10e-6;

if (catype==1) dscaeg = 2;		/* set Ca cooperativity = 2 */

at [aii] sphere dia 4 rm=1e8 vrev -.07 vrest -0.07;

conn [rbc] to [aii] synapse maxcond 4e-10 
                timec2 [2]
		// expon 3		/* generic synapse expon release gain */
		// thresh -0.05		/* generic synapse release threshold */
                sens Ca 
		rrpool   = maxrrpool
		mrrpool  = maxrrpool 
		maxsrate = maxsynrate
		trconc   = 10e-4
		vesnoise = vnoise
                resp ampa type 5 taum=1 tauh=1 chnoise=0 N=100 
                ename rbsyn;


expt = 1;

vhold    = -0.10;
vpulse   = -0.05;
tailvolt = -0.12;
if (notinit(vstep))   vstep =  0.002;

if (notinit(predur))           predur = 0.1;
if (notinit(prestimdur))   prestimdur = 0.01;
if (notinit(stimdur))         stimdur = 0.20;
if (notinit(tailcurdur))   tailcurdur = 0.001;
if (notinit(poststimdur)) poststimdur = 0.5;

/* Experiment to test activation at different voltages */


if (ivplot) {		/* make graph for I/V plot */
  plsep = 0;
  nonodes = 1;		/* turn off node numbers in display */
  if (notinit(vstart)) vstart = -0.07;
  if (notinit(vstop))   vstop = 0.02;
  graph X max 0.02 min vstart;
  graph Y max 25e-12 min -50e-12;	/* Ca current in RBC */
  graph Y max 20e-12 min -40e-12;	/* glutamate-gated current in AII */
  graph init;
  graph pen (magenta,green);
  glabel ("Irbc", magenta, 0.05, 0.70);
  glabel ("Iaii", green,   0.05, 0.67);
}
else {			// make separate V,I,Ca plots
  timinc = 1e-4;
  poststimdur = 0.2;
  plsep = 1;
  setxmin = 0;
  if (notinit(vstart)) vstart = -0.06;
  if (notinit(vstop))   vstop = -0.02;
  plot V[rbc] max -0.02 min vhold    plnum 4 plname "Vrbc";
  plot I[rbc] max 0e-12 min -20e-12  plnum 3 plname "Irbc";
  plot Ca[rbc] max 10e-6 min 0       plnum 2 plname "Ca_rbc" plsize 0.5;
  plot I[aii] max 0e-12 min -50e-12  plnum 1 plname "Iaii";
//  plot FA9  rbsyn max 200 min 0 plnum 5 plname "rbc_rate";
// plot G(1) rbsyn max 2 min 0;
// plot G(2) rbsyn max 2 min 0;
// plot G(3) rbsyn max 2 min 0;
};

endexp = prestimdur+stimdur+tailcurdur+poststimdur;

time = -predur;
stim node rbc vclamp=vhold start=time dur=predur;
stim node aii vclamp=vhold start=time dur=1e3;
step predur;
sprintf (savefile,"rbc_aii%06g.save",getpid());       // add pid to file name
save model (savefile);

for (vpulse=vstart,i=0; vpulse < vstop; vpulse += vstep,i++) {
  time = 0;
  Irbc = 0;
  Iaii = 0;
  Irec = 0;
  stim node rbc vclamp=vhold start=time dur=prestimdur;
  step prestimdur; 
  flag = 1;
  stim node rbc vclamp=vpulse start=prestimdur dur=stimdur;
  step stimdur; 
  flag = 0;
  if (ivplot) graph (vpulse,Irbc,Iaii);
  stim node rbc vclamp=tailvolt start=prestimdur+stimdur dur=tailcurdur;
  step tailcurdur; 
  stim node rbc vclamp=vhold start=prestimdur+stimdur+tailcurdur dur=poststimdur;
  step poststimdur; 
  restore model (savefile);
};

unlink (savefile);

